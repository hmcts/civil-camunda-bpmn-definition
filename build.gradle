plugins {
  id 'application'
  id 'checkstyle'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.12.RELEASE'
//  id 'org.springframework.boot' version '2.5.14'
  id 'org.owasp.dependencycheck' version '8.2.1'
  id 'com.github.ben-manes.versions' version '0.47.0'
  id 'org.sonarqube' version '3.4.0.2513'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

allprojects {
  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(17)
    }
  }

  apply plugin: 'java'
  apply plugin: 'jacoco'
  apply plugin: 'checkstyle'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'org.owasp.dependencycheck'

  checkstyle {
    maxWarnings = 0
    toolVersion = '10.4'
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
  }

  jacoco {
    toolVersion = '0.8.8' // jacocoMavenPluginVersion
    reportsDirectory = file("$buildDir/reports/jacoco")
  }

// before committing a change, make sure task still works
  dependencyUpdates {
    def isNonStable = { String version ->
      def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
      def regex = /^[0-9,.v-]+$/
      return !stableKeyword && !(version ==~ regex)
    }
    rejectVersionIf { selection -> // <---- notice how the closure argument is named
      return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
    }
  }

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
  dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'config/owasp/suppressions.xml'

    analyzers {
      // Disable scanning of .NET related binaries
      assemblyEnabled = false
    }
  }

  dependencyManagement {
    dependencies {
      // CVE-2018-10237 - Unbounded memory allocation
      dependency group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.69'
      // CVE-2020-26945 - Mishandles deserialization of object streams.
      dependency group: 'org.mybatis', name: 'mybatis', version: '3.5.13'
      dependency group: 'commons-io', name: 'commons-io', version: '2.10.0'
      dependency group: 'org.springframework.security', name: 'spring-security-crypto', version: '5.7.10'

      dependencySet(
        group: 'org.camunda.bpm',
        version: '7.19.0'
      ) {
        entry 'camunda-engine'
      }
      dependencySet(
        group: 'org.jetbrains.kotlin',
        version: '1.8.20'
      ) {
        entry 'kotlin-stdlib-common'
      }
      dependencySet(
        group: 'org.jetbrains.kotlin',
        version: '1.8.20'
      ) {
        entry 'kotlin-stdlib'
      }
      dependencySet(
        group: 'org.jetbrains.kotlin',
        version: '1.8.20'
      ) {
        entry 'kotlin-reflect'
      }

      dependencySet(group: 'com.squareup.okio', version: '3.4.0') {
        entry 'okio'
      }

    }
    imports {
      mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2021.0.6'
    }
  }

  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      url "https://jitpack.io"
    }
    jcenter()
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

task awaitApplicationReadiness(type: Exec, description: 'Awaits until application is ready.') {
  commandLine './bin/wait-for.sh', System.getenv('URL') ?: 'https://localhost:4100'
  commandLine './bin/wait-for.sh', System.getenv('TEST_URL')?: 'https://localhost:4100'
}

task runCosTests(type: Exec, description: 'Runs DJ Api functional tests.') {
  onlyIf {
    return System.env.ENVIRONMENT == 'preview'
  }
  commandLine '/usr/bin/yarn', '--silent', 'run', 'test:api-cos'
}

task runSdoApiTests(type: Exec, description: 'Runs SDO functional tests.') {
  onlyIf {
    return System.env.ENVIRONMENT == 'preview'
  }
  commandLine '/usr/bin/yarn', '--silent', 'run', 'test:api-sdo'
}

task runSmokeTests(type: Exec, description: 'Runs smoke tests.') {
  commandLine '/usr/bin/yarn', '--silent', 'run', 'test:smoke'
}

task runDJTests(type: Exec, description: 'Runs DJ Api functional tests.') {
  commandLine '/usr/bin/yarn', '--silent', 'run', 'test:api-dj'
}

task runSpecApiTests(type: Exec, description: 'Runs Spec functional tests.') {
  commandLine '/usr/bin/yarn', '--silent', 'run', 'test:api-spec'
}

task runApiTests(type: Exec, description: 'Runs Api functional tests.') {
  commandLine '/usr/bin/yarn', '--silent', 'run', 'test:api-unspec'
}


task runRpaHandOffTests(type: Exec, description: 'Runs functional tests.') {
  commandLine '/usr/bin/yarn', '--silent', 'run', 'test:rpa'
}

task runNightlyMultipartyTests(type: Exec, description: 'Runs additional multiparty nightly tests.') {
  commandLine '/usr/bin/yarn', '--silent', 'run', 'test:nightly-multiparty'
}

def inStrictOrder(Task... tasks) {
  for (int i = 0; i < tasks.size() - 1; i++) {
    tasks[i + 1].mustRunAfter(tasks[i])
  }
  return tasks
}

task smoke(description: 'Runs the smoke tests.') {
  dependsOn(inStrictOrder(awaitApplicationReadiness, runSmokeTests))
}

task functional(description: 'Runs the functional tests.') {
  dependsOn(inStrictOrder(awaitApplicationReadiness,
    runCosTests, runDJTests, runSpecApiTests, runSdoApiTests, runApiTests))
}

project.tasks['sonarqube'].dependsOn test, jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "CIVIL :: camunda-bpmn-definition"
    property "sonar.projectKey", "civil-camunda-bpmn-definition"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
    property "sonar.host.url", "https://sonar.reform.hmcts.net/"
  }
}

jacocoTestReport {
  executionData(test)
  executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
  subprojects.each {
    sourceSets it.sourceSets.main
  }
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

jacocoTestReport.dependsOn {
  subprojects*.test
}

def versions = [
  junit              : '5.9.2',
  junitPlatform      : '1.9.2'
]

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}"
  ]
]

dependencies {
  implementation group: 'org.springframework', name: 'spring-core', version: '5.3.27'
  implementation group: 'org.springframework', name: 'spring-beans', version: '5.3.27'
  implementation group: 'org.camunda.bpm', name: 'camunda-external-task-client', version: '7.18.0'
  implementation group: 'org.camunda.bpm.extension.rest', name: 'camunda-rest-client-spring-boot-starter', version: '0.0.6'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.2'
  implementation group: 'com.fasterxml.jackson', name: 'jackson-bom', version: '2.14.0-rc1', ext: 'pom'
  implementation 'org.yaml:snakeyaml:2.0'

  testImplementation libraries.junit5
  testImplementation group: 'org.camunda.bpm.extension.springboot', name: 'camunda-bpm-spring-boot-starter-test', version: '2.2.0'
  testImplementation group: 'com.h2database', name: 'h2', version: '1.4.200'
    testImplementation 'junit:junit:4.13.1'
}


jar {
  getArchiveFileName().set(provider {
    'empty.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}

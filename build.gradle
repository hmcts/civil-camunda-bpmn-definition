plugins {
  id 'application'
  id 'checkstyle'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.7'
//  id 'org.springframework.boot' version '2.5.14'
  id 'org.owasp.dependencycheck' version '9.2.0'
  id 'com.github.ben-manes.versions' version '0.52.0'
  id 'org.sonarqube' version '3.5.0.2730'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

allprojects {
  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(17)
    }
  }

  apply plugin: 'java'
  apply plugin: 'jacoco'
  apply plugin: 'checkstyle'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'org.owasp.dependencycheck'

  checkstyle {
    maxWarnings = 0
    toolVersion = '10.26.1'
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
  }

  jacoco {
    toolVersion = '0.8.13' // jacocoMavenPluginVersion
    reportsDirectory = file("$buildDir/reports/jacoco")
  }

// before committing a change, make sure task still works
  dependencyUpdates {
    def isNonStable = { String version ->
      def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
      def regex = /^[0-9,.v-]+$/
      return !stableKeyword && !(version ==~ regex)
    }
    rejectVersionIf { selection -> // <---- notice how the closure argument is named
      return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
    }
  }

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
  dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'config/owasp/suppressions.xml'

    analyzers {
      // Disable scanning of .NET related binaries
      assemblyEnabled = false
    }
  }

  dependencyManagement {
    dependencies {

      dependency group: 'commons-io', name: 'commons-io', version: '2.20.0'
      dependency group: 'org.springframework.security', name: 'spring-security-crypto', version: '5.8.16'

      //Solves CVE-2023-33202
      dependency group: 'org.springframework.security', name: 'spring-security-rsa', version: '1.1.5'


      dependencySet(
        group: 'org.camunda.bpm',
        version: '7.23.0'
      ) {
        entry 'camunda-engine'
      }
      dependencySet(
        group: 'org.jetbrains.kotlin',
        version: '1.9.25'
      ) {
        entry 'kotlin-stdlib-common'
        entry 'kotlin-stdlib'
        entry 'kotlin-reflect'

      }

      dependencySet(group: 'com.squareup.okio', version: '3.15.0') {
        entry 'okio'
      }

      // Solves CVE-2023-20883
      dependencySet(group: 'org.springframework.boot', version: '2.7.18'){
        entry 'spring-boot-autoconfigure'
        entry 'spring-boot-starter'
        entry 'spring-boot-starter-aop'
        entry 'spring-boot-starter-logging'
      }

      //Solves CVE-2023-0833
      dependency group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.12.0'

      //Solves CVE-2023-6378
      dependencySet(group: 'ch.qos.logback', version: '1.5.18'){
        entry 'logback-classic'
        entry 'logback-core'
      }


      //Solves CVE-2023-24998
      dependency group: 'commons-fileupload', name: 'commons-fileupload', version: '1.6.0'


    }
    imports {
      mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2021.0.9'
    }
  }

  repositories {
    mavenLocal()
    mavenCentral()
  }
}

tasks.withType(JavaCompile).configureEach {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

tasks.register('awaitApplicationReadiness', Exec) {
  commandLine './bin/wait-for.sh', System.getenv('URL') ?: 'https://localhost:4100'
  commandLine './bin/wait-for.sh', System.getenv('TEST_URL') ?: 'https://localhost:4100'
}

tasks.register('runApiTests', Exec) {
  onlyIf {
    return System.env.NIGHTLY_RUN != 'true'
  }
  commandLine './bin/run-api-tests.sh'
}

tasks.register('runNightlyApiTests', Exec) {
  onlyIf {
    return System.env.NIGHTLY_RUN == 'true'
  }
  commandLine './bin/run-nightly-api-tests.sh'
}

tasks.register('runSmokeTests', Exec) {
  commandLine '/usr/bin/yarn', '--silent', 'run', 'test:smoke'
}

tasks.register('runRpaHandOffTests', Exec) {
  commandLine '/usr/bin/yarn', '--silent', 'run', 'test:rpa'
}

tasks.register('runNightlyMultipartyTests', Exec) {
  commandLine '/usr/bin/yarn', '--silent', 'run', 'test:nightly-multiparty'
}

static def inStrictOrder(Task... tasks) {
  for (int i = 0; i < tasks.size() - 1; i++) {
    tasks[i + 1].mustRunAfter(tasks[i])
  }
  return tasks
}

tasks.register('smoke') {
  dependsOn(inStrictOrder(awaitApplicationReadiness, runSmokeTests))
}

tasks.register('functional') {
  dependsOn(inStrictOrder(awaitApplicationReadiness,
    runApiTests, runNightlyApiTests))
}

project.tasks['sonarqube'].dependsOn test, jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "CIVIL :: camunda-bpmn-definition"
    property "sonar.projectKey", "civil-camunda-bpmn-definition"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
    property "sonar.host.url", "https://sonar.reform.hmcts.net/"
  }
}

jacocoTestReport {
  executionData(test)
  executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
  subprojects.each {
    sourceSets it.sourceSets.main
  }
  reports {
    xml.required = true
    csv.required = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

jacocoTestReport.dependsOn {
  subprojects*.test
}

def versions = [
  junit              : '5.13.4',
  junitPlatform      : '1.13.4'
]

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}"
  ]
]

dependencies {
  implementation group: 'org.springframework', name: 'spring-core', version: '5.3.39'
  implementation group: 'org.springframework', name: 'spring-beans', version: '5.3.39'
  implementation group: 'org.camunda.bpm', name: 'camunda-external-task-client', version: '7.23.0'
  implementation group: 'org.camunda.bpm.extension.rest', name: 'camunda-rest-client-spring-boot-starter', version: '0.0.6'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.19.2'
  implementation group: 'com.fasterxml.jackson', name: 'jackson-bom', version: '2.19.2', ext: 'pom'
  implementation 'org.yaml:snakeyaml:2.4'

  testImplementation libraries.junit5
  testImplementation group: 'org.camunda.bpm.extension.springboot', name: 'camunda-bpm-spring-boot-starter-test', version: '2.2.0'
  testImplementation group: 'com.h2database', name: 'h2', version: '1.4.200'
    testImplementation 'junit:junit:4.13.2'
}


jar {
  getArchiveFileName().set(provider {
    'empty.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

static def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}
